{Module
  Demo/Flow

  {Import Core/Main as C open}
  {Import Core/Plumb as CP open macro}
  {Import Core/Effect as CE open}
  {Import Core/KV as KV}

  {Program
    App(
      State(
        Vars(
          {KV Input Empty},
          {KV Name  Empty},
          {KV Age   Empty}
        )
      )
    )
    {Flow
      Idle
      {Print "Enter your name: \n"}
      {ReadLine}
      {Store Name}
      {Print+ Name}
      {Print "Enter your age: \n"}
      {ReadLine}
      {Store Age}
      {Print+ Age}
    }
    {EffQueue}
    {Effects
      {Pending}
      {Inbox}
    }
  }
  {Rules
    R(
      "Flow/Print",
      {Plumb {Flow Idle {Print msg_} flow...} {EffQueue}},
      {PlumbEnd {Flow Idle flow...} {EffQueue {Print msg_}}}
    )
    R(
      "Flow/ReadLine",
      {Plumb {Flow Idle {ReadLine} flow...} {EffQueue}},
      {PlumbEnd {Flow Busy flow...} {EffQueue {ReadLine}}}
    )
    R(
      "Flow/Store",
      {Plumb {App{State vars_}} {Flow Idle {Store where_} flow...}},
      {PlumbEnd {App{State KV/Copy(Vars, Input, where_, vars_)}} {Flow Idle flow...}}
    )

    R("Answer/Name", {Answer Name}, "Hello ")
    R("Answer/Age",  {Answer Age},  "Your age is ")

    R(
      "Flow/PrintName",
      {Plumb {App{State vars_}} {Flow Idle {Print+ n_} flow...}},
      {PlumbEnd {App{State vars_}} {Flow Idle {Print Concat(Answer(n_), KV/Get(Vars, n_, vars_), "!\n")} flow...}}
    )

    R(
      "ProcessReadLine",
      {Plumb
        {App{State vars_ st...}}
        {Flow idle_ flow...}
        omit...
        {Effects pending_ {Inbox {ReadLineComplete id_ {Text input_}} rest...}}
      },
      {PlumbEnd
        {App{State KV/Put(Vars, Input, input_, vars_) st...}}
        {Flow Idle flow...}
        omit...
        {Effects pending_ {Inbox rest...}}
      }
    )
  }
}