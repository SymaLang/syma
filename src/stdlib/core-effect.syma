{Module
  Core/Effect
  {Import Core/Plumb as CP open macro}
  {Export PrintEff}
  {Rules
    R("EnqueueEffect/Generic",
      {Plumb {EffQueue {Emit eff_} cmds...} {Effects {Pending pend...} inbox_}},
      {PlumbEnd {EffQueue cmds...} {Effects {Pending pend... eff_} inbox_}}
    )
    R(
      "EnqueueEffect/Print",
      {Plumb {EffQueue {Print m_} rest...}},
      {PlumbEnd {EffQueue {Emit {PrintEff m_}} rest...}}
    )
    R(
      "EnqueueEffect/ReadLine",
      {Plumb {EffQueue {ReadLine} rest...}},
      {PlumbEnd {EffQueue {Emit {ReadLineEff}} rest...}}
    )
    R(
      "PrintEff/New",
      {PrintEff m_},
      {Print FreshId() {Message m_}}
    )
    R(
      "ReadLineEff/New",
      {ReadLineEff},
      {ReadLine FreshId()}
    )
    R(
      "PrintComplete",
      {Plumb
        {Effects
          pending_
          {Inbox {PrintComplete id_ Success} rest...}
        }
      },
      {PlumbEnd {Effects pending_ {Inbox rest...}}}
    )
  }
}